# Full Example
# https://brouter.de/brouter-web/#map=14/49.2853/-123.1407/standard&lonlats=-123.126559,49.289796;-123.097153,49.278784;-123.124914,49.279419;-123.135149,49.291143;-123.156363,49.303125;-123.148831,49.272573

---context:global
assign validForBikes      = true
assign processUnusedTags  = false # %processUnusedTags% | Set true to output unused tags in data tab | boolean

---context:way

# Consider only public land paths, plus ferry

assign impracticality_penalty =
  if (
    or highway=proposed|abandoned|motorway|motorway_link
    or motorroad=yes
    or access=private|no
    or vehicle=private|no
    ( and ( not route=ferry ) highway= )
  ) then 10000
  else 0

# Avoid pedestrian infrastructure
# Adjust based on:
# - Rawlings Trail vs Stanley Park Seawall Bike Path
#   https://brouter.de/brouter-web/#map=16/49.2973/-123.1481/standard&lonlats=-123.145752,49.295477;-123.150858,49.296597
# - Seaside Path vs Beach Avenue Lane
#   https://brouter.de/brouter-web/#map=15/49.2898/-123.1474/standard&lonlats=-123.150913,49.296269;-123.135583,49.277691
# - Convention Center stairs vs Seawall lane

assign pedestrian_penalty =
  switch highway=steps                10
  switch bicycle=no                   3
  switch bicycle=dismount             3
  switch highway=footway              2
  0

# Avoid shared lanes and painted gutters, prefer cycleways
# Adjust based on:
# - Haro & Bidwell to Safeway on Robson (should prefer Denman St)
#   https://brouter.de/brouter-web/#map=18/49.29034/-123.13538/standard&lonlats=-123.134674,49.289597;-123.135149,49.291143

assign cycling_infrastructure_safety_penalty =
  switch highway=cycleway|footway             0
  switch living_street=yes                    0.10
  switch highway=residential|living_street    0.10
  switch cycleway=shared_lane                 0.15
  switch highway=tertiary|tertiary_link       0.15
  switch highway=secondary|secondary_link     0.20
  switch highway=primary|primary_link         0.50
  1

assign one_way_penalty =
  if and reversedirection=yes ( or oneway:bicycle=yes oneway=yes|true|1 )
  then 20
  else 0

# Tiny cost adjustments for route niceties

assign traffic_penalty
  switch estimated_traffic_class=7|6    0.05
  switch estimated_traffic_class=5      0.04
  switch estimated_traffic_class=4      0.03
  switch estimated_traffic_class=3      0.02
  switch estimated_traffic_class=1|2    0.01
                                        0.00

assign noise_penalty
  switch estimated_noise_class=5|6      0.05
  switch estimated_noise_class=4        0.04
  switch estimated_noise_class=3        0.03
  switch estimated_noise_class=2        0.02
  switch estimated_noise_class=1        0.01
                                        0.00

assign no_river_penalty
  switch estimated_river_class=6|5      0.00
  switch estimated_river_class=4        0.01
  switch estimated_river_class=3        0.02
  switch estimated_river_class=2        0.03
  switch estimated_river_class=1        0.04
                                        0.05

assign no_forest_penalty
  switch estimated_forest_class=6|5     0.00
  switch estimated_forest_class=4       0.01
  switch estimated_forest_class=3       0.02
  switch estimated_forest_class=2       0.03
  switch estimated_forest_class=1       0.04
                                        0.05

# calculate the cost-factor, which is the factor by which the distance of a
# way-segment is multiplied to calculate the cost of that segment. The
# costfactor must be >=1 and it's supposed to be close to 1 for the type of
# way the routing profile is searching for

assign costfactor
  # fixed cost factor for taking the ferry
  if route=ferry then 2 else

  # penalties to encourage bike paths
  add impracticality_penalty
  add pedestrian_penalty
  
  # penalties to encourage really nice bike paths
  add cycling_infrastructure_safety_penalty

  add no_river_penalty
  add noise_penalty
  add traffic_penalty
  add one_way_penalty
  add no_forest_penalty
  1

---context:node  # following code refers to node tags

assign defaultaccess =
       if ( access= ) then true # add default barrier restrictions here!
       else if ( access=private|no ) then false
       else true

assign bike_access =
       if nodeaccessgranted=yes then true
       else if bicycle= then
       (
         if vehicle= then defaultaccess
         else not vehicle=private|no
       )
       else not bicycle=private|no|dismount

assign footaccess =
       if bicycle=dismount then true
       else if foot= then defaultaccess
       else not foot=private|no

assign initialcost =
       if bike_access then 0
       else ( if footaccess then 100 else 1000000 )
